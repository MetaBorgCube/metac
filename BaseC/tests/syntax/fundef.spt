module fundef

language metac

start symbol Program

test minimal function definition [[
  x() {}
]] run get-fundef-from-program to FunDef(
  [],
  FunDeclarator(
    None,
    Identifier("x"),
    ParameterList([])
  ),
  [],
  CompoundStatement([])
)

test parenthesized function name [[
  (x)() {}
]] run get-fundef-from-program to FunDef(
  [],
  FunDeclarator(
    None,
    ParenName(Identifier("x")),
    ParameterList([])
  ),
  [],
  _
)

test function with one parameter
  <parse-string ; disamb; get-fundef-from-program> ("
    x (int32 a) {}
  ") => FunDef(
    [],
    FunDeclarator(
      None,
      Identifier("x"),
      ParameterList([
        Parameter([Type(Int32)], Declarator(None, Identifier("a")))
      ])
    ),
    [],
    CompoundStatement([])
  )

test old style declaration [[
  void x(a, b) int32 a, b; {

  }
]] parse succeeds

test function without parameter name [[x (int32) {}]] parse fails
test function without parameter name [[x (const int32) {}]] parse fails

test function return type [[
  int32 x () {}
]] run get-fundef-from-program to FunDef(
  [Type(Int32)],
  FunDeclarator(
    None,
    Identifier("x"),
    ParameterList([])
  ),
  [],
  CompoundStatement([])
)

test function complicated return type [[
  const static int32 * const x() {}
]] run get-fundef-from-program to FunDef(
  [Const, Static, Type(Int32)],
  FunDeclarator(
    Some(Pointer([Const], None)),
    Identifier("x"),
    ParameterList([])
  ),
  _,
  _
)
