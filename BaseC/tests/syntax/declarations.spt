module declarations
language metac

start symbol Program

test just a useless declaration [[
  a;
]] run get-decl-from-program to Declaration(
  [], [Declarator(None, Identifier("a"))]
)

test declaration cannot be a keyword [[
  int32;
]] parse fails

test declaration cannot be another keyword (type qualifier) [[
  const;
]] parse fails

test declaration cannot be another keyword (storage class) [[
  volatile;
]] parse fails

test a single variable declaration [[
  int32 a;
]] run get-decl-from-program to Declaration(
  [Type(Int32)],
  [Declarator(None(), Identifier("a"))]
)

test multiple variable declarations [[
  int32 a, b;
]] run get-decl-from-program to Declaration(
  [Type(Int32)],
  [
    Declarator(None(), Identifier("a")),
    Declarator(None(), Identifier("b"))
  ]
)

test const int [[
  const int32 a;
]] run get-decl-from-program to Declaration(
  [Const, Type(Int32)],
  _
)

test int const [[
  int32 const a;
]] run get-decl-from-program to Declaration(
  [Type(Int32), Const],
  _
)

test pointer int [[
  int32 * a;
]] run get-decl-from-program to Declaration(
  [Type(Int32)],
  [
    Declarator(
      Some(Pointer([], None)),
      Identifier("a")
    )
  ]
)

test two varables of which one is a pointer [[
  int32 a, * b;
]] run get-decl-from-program to Declaration(
  [Type(Int32)],
  [
    Declarator(None, _),
    Declarator(Some(_), _)
  ]
)

test variable with init expression [[
  int32 a = 1;
]] run get-decl-from-program to Declaration(
  [Type(Int32)],
  [
    InitDeclarator(
      Declarator(_, _),
      _
    )
  ]
)

test double pointer [[
  int32 ** a;
]] run get-decl-from-program to Declaration(
  [Type(Int32)],
  [
    Declarator(
      Some(Pointer([], Some(Pointer([], None)))),
      Identifier("a")
    )
  ]
)

test int * const volatile [[
  int32 * const volatile a;
]] run get-decl-from-program to Declaration(
  [Type(Int32)],
  [
    Declarator(
      Some(Pointer([Const, Volatile], None)),
      _
    )
  ]
)

test int with storage class [[
  static const extern int32 * a;
]] run get-decl-from-program to Declaration(
  [
    Static,
    Const,
    Extern,
    Type(Int32)
  ],
  [
    Declarator(
      Some(Pointer([], None)),
      _
    )
  ]
)

test declaration with parens [[
  int32 (* a);
]] run get-decl-from-program to Declaration(
  [Type(Int32)],
  [
    Declarator(
      None,
      ParensDecl(Declarator(
        Some(Pointer([], None)),
        Identifier("a")
      ))
    )
  ]
)

test array decl [[
  int32 a[];
]] run get-decl-from-program to Declaration(
  [Type(Int32)],
  [
    Declarator(
      None,
      ArrayDecl(
        Identifier("a"),
        None
      )
    )
  ]
)

test typedef [[
  typedef int32 a;
]] run get-decl-from-program to Declaration(
  [Typedef, Type(Int32)],
  [Declarator(
    None,
    Identifier("a")
  )]
)

test a typedefed decl [[
  a b, c;
]] run get-decl-from-program to Declaration(
  [Type(Identifier("a"))],
  [
    Declarator(None, Identifier("b")),
    Declarator(None, Identifier("c"))
  ]
)
