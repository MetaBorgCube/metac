module structs

language Metac

start symbol Program

test struct declaration [[
  struct x;
]] parse to Declarations([
    Declaration(
      [Struct(Identifier("x"))],
      _
    )
  ])

test one element struct [[
  struct {
    int32 x;
  };
]] parse to Declarations([
    Declaration(
      [Struct(None, [
        StructDeclaration(
          [Int32],
          [Declarator(None, Identifier("x"))]
        )
      ])],
      _
    )
  ])

test more elements struct [[
  struct {
    int32 x;
    int32 *y;
  };
]] parse to Declarations([
    Declaration(
      [Struct(None, [
        StructDeclaration(
          [Int32],
          [Declarator(None, Identifier("x"))]
        ),
        StructDeclaration(
          [Int32],
          [Declarator(Some(Pointer([], None)), Identifier("y"))]
        )
      ])],
      []
    )
  ])

test named struct [[
  struct X {
    int32 x;
  };
]] parse to Declarations([
    Declaration(
      [Struct(Some(Identifier("X")), [_])],
      []
    )
  ])

test typedef'ed named struct [[
  typedef struct Y {
    int32 x;
  } X;
]] parse to Declarations([
    Declaration(
      [
        Typedef(),
        Struct(Some(_), [_])
      ],
      [Declarator(None(), Identifier("X"))]
    )
  ])


test custom type declarator [[
  typedef int32 x;
  struct X {
    const x y;
  };
]] parse to Declarations([
    _,
    Declaration([
      Struct(Some(Identifier("X")), [
        StructDeclaration(
          [Const(), TypedefName(Identifier("x"))],
          [Declarator(None, Identifier("y"))]
        )
      ])
    ], _)
  ])

test multiple declarators [[
  typedef int32 z;
  struct X {
    const x y, z;
  };
]] parse to Declarations([
    _,
    Declaration([
      Struct(Some(Identifier("X")), [
        StructDeclaration(
          [Const(), TypedefName(Identifier("x"))],
          [
            Declarator(None, Identifier("y")),
            Declarator(None, Identifier("z"))
          ]
        )
      ])
    ], _)
  ])

test multiple declarators with bit-size [[
  typedef int32 z;
  struct X {
    const x y, z : 3;
  };
]] parse to Declarations([
    _,
    Declaration([
      Struct(Some(Identifier("X")), [
        StructDeclaration(
          [Const(), TypedefName(Identifier("x"))],
          [
            Declarator(None, Identifier("y")),
            StructSizedFieldDeclarator(
              Some(Declarator(None, Identifier("z"))),
              Integer("3")
            )
          ]
        )
      ])
    ], _)
  ])
