module BaseC-names

imports

  include/Metac
  BaseC/trans/desugar/-

namespaces

  Function
  Variable
  Typedef

binding rules

  // file
  Declarations(_):
    scopes Function, Variable, Typedef

  // Typedefs

  TypedefDeclaration(
    type,
    Declarator(_, _),
    Decl(Identifier(name))
  ) :
    defines unique Typedef name

  TypedefName(Identifier(name)):
    refers to Typedef name

  // Variables

  VarDeclaration(storage, types, declarator, Decl(Identifier(name)), _):
    defines unique Variable name
    of type types

  Var(Identifier(name)):
    refers to Variable name
    otherwise refers to Function name

  // Functions

  FuncDecl(Identifier(name), _):
    defines unique Function name

  FunDefinition(Identifier(name), storage, types, parameters, body):
    defines unique Function name
    scopes Variable, Typedef

  FunDefinitionParameter(types, _, Decl(Identifier(name))):
    defines unique Variable name
    of type types

  CompoundStatement(_):
    scopes Variable, Typedef


