module BaseC-Expr

imports

  Common
  BaseC-Constants
  BaseC-Declarations
  BaseC-Identifiers

context-free syntax

  // expression
  Expr =
    <(<Expr>)> {bracket}

  Expr.Comma =
    <<{AExpr ","}+>> {avoid}

  Expr =
    AExpr {prefer}


  AExpr = <(<AExpr>)> {bracket}


  // assignment-expression
  AExpr.Assign =
    <<UnaryExpr> <AssignOp> <AExpr>>

  // assignment-operator
  AssignOp.Assign =
    <=>
  AssignOp.PlusAssign =
    <+=>
  AssignOp.MinusAssign =
    <-=>
  AssignOp.MultiplyAssign =
    <*=>
  AssignOp.DivisionAssign =
    </=>
  AssignOp.ModuloAssign =
    <%=>
  AssignOp.BitwiseAndAssign =
    <&=>
  AssignOp.BitwiseXorAssign =
    <^=>
  AssignOp.BitwiseOrAssign =
    <|=>
  AssignOp.BitshiftLeftAssign =
    [<<=]
  AssignOp.BitshiftRightAssign =
    [>>=]


  // conditional-expression
  // TODO Fix me
  AExpr.Conditional =
    <<AExpr> ? <AExpr> : <AExpr>> {right}


  // logical-OR-expression
  AExpr.LogicalOr =
    <<AExpr> || <AExpr>> {left}


  // logical-AND-expression
  AExpr.LogicalAnd =
    <<AExpr> && <AExpr>> {left}

  // inclusive-OR-expression
  AExpr.InclusiveOr =
    <<AExpr> | <AExpr>> {left}

  // exclusive-OR-expression
  AExpr.ExclusiveOr =
    <<AExpr> ^ <AExpr>> {left}

  // AND-expression
  AExpr.InclusiveAnd =
    <<AExpr> & <AExpr>> {left}

  // equality-expression
  AExpr.Equal =
    <<AExpr> == <AExpr>> {left}

  AExpr.InEqual =
    <<AExpr> != <AExpr>> {left}

  // relational-expression
  AExpr.Lt =
    [[AExpr] < [AExpr]] {left}

  AExpr.Gt =
    [[AExpr] > [AExpr]] {left}

  AExpr.Lte =
    [[AExpr] <= [AExpr]] {left}

  AExpr.Gte =
    [[AExpr] >= [AExpr]] {left}

  // shift-expression
  AExpr.ShiftLeft =
    [[AExpr] << [AExpr]] {left}

  AExpr.ShiftRight =
    [[AExpr] >> [AExpr]] {left}

  // additive-expression
  AExpr.Add =
    <<AExpr> + <AExpr>> {left}


  AExpr.Subtract =
    <<AExpr> - <AExpr>> {left}

  // multiplicative-expression
  AExpr.Mult =
    <<AExpr> * <AExpr>> {left}

  AExpr.Div =
    <<AExpr> / <AExpr>> {left}

  AExpr.Mod =
    <<AExpr> % <AExpr>> {left}

  // cast-expression
  AExpr.Cast =
    <(<TypeName>) <AExpr>>


  AExpr =
    UnaryExpr

  // unary-expression
  UnaryExpr.IncrementPrefix =
    <++<AExpr>> {right}

  UnaryExpr.DecrementPrefix =
    <--<AExpr>> {right}

  UnaryExpr.Address =
    <&<AExpr>> {right}

  UnaryExpr.Deref =
    <*<AExpr>> {right}

  UnaryExpr.Positive =
    <+<AExpr>> {right}

  UnaryExpr.Negative =
    <-<AExpr>> {right}

  UnaryExpr.Complement =
    <~<AExpr>> {right}

  UnaryExpr.Negate =
    <!<AExpr>> {right}

  UnaryExpr =
    PostFixExpr


  // postfix-expression

  PostFixExpr.ArrayField =
    <<AExpr>[<AExpr>]>

  PostFixExpr.Call =
    <<AExpr>(<{AExpr ","}*>)>

  PostFixExpr.Field =
    <<AExpr>.<Identifier>>

  PostFixExpr.PointerField =
    [[AExpr]->[Identifier]]

  PostFixExpr.Increment =
    <<AExpr>++> {left}

  PostFixExpr.Decrement =
    <<AExpr>--> {left}

  PostFixExpr =
    PrimaryExpr


  // primary-expression
  PrimaryExpr =
    Identifier

  PrimaryExpr =
    Constant {prefer}

  PrimaryExpr.String =
    STRING


context-free priorities
  { left:
      AExpr.Cast
      PostFixExpr.Increment
      PostFixExpr.Decrement
      PostFixExpr.ArrayField
      PostFixExpr.Call
      PostFixExpr.Field
      PostFixExpr.PointerField
  } >
  { right:
      UnaryExpr.IncrementPrefix
      UnaryExpr.DecrementPrefix
      UnaryExpr.Address
      UnaryExpr.Deref
      UnaryExpr.Positive
      UnaryExpr.Negative
      UnaryExpr.Complement
      UnaryExpr.Negate
  } >
  { left:
      AExpr.Mult
      AExpr.Div
      AExpr.Mod
  } >
  { left:
      AExpr.Add
      AExpr.Subtract
  } >
  { left:
      AExpr.ShiftLeft
      AExpr.ShiftRight
  } >
  { left:
      AExpr.Gt
      AExpr.Gte
      AExpr.Lt
      AExpr.Lte
  } >
  {
    left:
      AExpr.Equal
      AExpr.InEqual
  } >
  { left: AExpr.InclusiveAnd } >
  { left: AExpr.ExclusiveOr } >
  { left: AExpr.InclusiveOr } >
  { left: AExpr.LogicalAnd } >
  { left: AExpr.LogicalOr } >
  { right: AExpr.Conditional } >
  { right: AExpr.Assign } >
  { left: Expr.Comma }

lexical restrictions
  "+" -/- [\+]
  "-" -/- [\-]
  "&" -/- [\&]
