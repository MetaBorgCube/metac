module sm

language MetaC

test parse empty sm [[
  statemachine Name {
    init start;
    state start {
    }
  }
]] parse to Program(_, [
    StateMachine(
      Identifier("Name"),
      InitState(Identifier("start")),
      [],
      [State(Identifier("start"), None(), [])]
    )
  ])

test statemachine with a local variable decl [[
  statemachine Name {
    init start;
    int32 x;
    state start {
    }
  }
]] parse to Program(_, [StateMachine(_, _,
    [Declaration([Int32], [Declarator(None, Decl(Identifier("x")))])],
    [_]
  )])

test statemachine with an epsilon transition [[
  statemachine Name {
    init start;
    state start {
      on -> finish;
    }
    state finish {}
  }
]] parse to Program(_, [StateMachine(_, _, [],
    [
      State(Identifier("start"), None(), [
        Transition(None(), None(), None(), Identifier("finish"))
      ]),
      State(Identifier("finish"), None(), [])
    ]
  )])


test statemachine with an epsilon transition with guard [[
  statemachine Name {
    init start;
    state start {
      on [1 < 3] -> finish;
    }
    state finish {}
  }
]] parse to Program(_, [StateMachine(_, _, [],
    [
      State(Identifier("start"), None(), [
        Transition(None(), Guard(Lt(_, _)), None(), Identifier("finish"))
      ]),
      State(Identifier("finish"), None(),[])
    ]
  )])

test statemachine with an epsilon transition with effect [[
  statemachine Name {
    init start;
    state start {
      on / printf("x"); -> finish;
    }
    state finish {}
  }
]] parse to Program(_, [StateMachine(_, _, [],
    [
      State(Identifier("start"), None(), [
        Transition(None(), None, Effect(ExpressionStatement(_)), Identifier("finish"))
      ]),
      State(Identifier("finish"), None(), [])
    ]
  )])

test statemachine with transition [[
  statemachine Name {
    init start;
    event foo(int32 a, int32 b);
    event bar();
    state start {
      on foo -> finish;
      on bar -> finish;
    }
    state finish {}
  }
]] parse to Program(_, [StateMachine(_, _,
    [
      Event(
        Identifier("foo"),
        ParameterList([
          Parameter([Int32], Declarator(None, Decl(Identifier("a")))),
          Parameter([Int32], Declarator(None, Decl(Identifier("b"))))
        ])
      ),
      Event(Identifier("bar"), ParameterList([]))
    ],
    [
      State(Identifier("start"), None(), [
        Transition(Some(Identifier("foo")), None, None, Identifier("finish")),
        Transition(Some(Identifier("bar")), None, None, Identifier("finish"))
      ]),
      State(Identifier("finish"), None(), [])
    ]
  )])

test statemachine with mixed events and declarations [[
  statemachine Name {
    init start;
    event a();
    int32 a;
    int32 b;
    event b();
    state start {}
  }
]] parse to Program(_, [StateMachine(_, _,
    [
      Event(Identifier("a"), ParameterList([])),
      Declaration(_, _),
      Declaration(_, _),
      Event(Identifier("b"), ParameterList([]))
    ],
    [
      State(Identifier("start"), None(), [])
    ]
  )])

test state with entry action [[
  statemachine X {
    init start;
    state start {
      entry {
        1;
      }
    }
  }
]] parse to Program(_, [StateMachine(_, _, [], [
    State(
      Identifier("start"),
      Entry(CompoundStatement([
        ExpressionStatement(Some(Integer("1")))
      ])),
      []
    )
  ])])

test state machine construction [[
  statemachine X {
    init start;
    int32 a = 1;
    event foo();
    state start {}
  }
  void main() {
    statemachine X x;
    x<|foo();
    int32 a = x|>a;
  }
]] parse to Program(_, [
    StateMachine(_, _, _, _),
    FunDef(
      _,
      FunDeclarator(None, Identifier("main"), _),
      [],
      CompoundStatement([
        Declaration(_, _),
        ExpressionStatement(Some(
          StateMachineTriggerEvent(Var(Identifier("x")),Identifier("foo"),[])
        )),
        Declaration(
          [Int32],
          [InitDeclarator(Declarator(None,Decl(Identifier("a"))),
            StateMachineField(Var(Identifier("x")), Identifier("a"))
          )]
        )
      ])
    )
  ])
