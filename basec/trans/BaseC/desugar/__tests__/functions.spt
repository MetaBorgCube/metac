module functions

language MetaC

test desugar parameter [[
  extern unsigned const int g(unsigned volatile int a) {
    return a;
  }
]] run strip-annos to Program(_, [
    FunDefinition(
      Identifier(_),
      [Extern()],
      Type([], Function(
        Type([Const()], UInt32()),
        [Type([Volatile()], UInt32())])
      ),
      [FunDefinitionParameter(
        Type([Volatile], UInt32),
        Decl(Identifier(_))
      )],
      CompoundStatement(_)
    )
  ])

test desugar parameter type with pointer [[
  int32 g(int32 *x) {
    return &x;
  }
]] run strip-annos to Program(_, [
    FunDefinition(
      Identifier(_),
      [],
      Type([], Function(
        Type([], Int32),
        [Type([], Pointer(Int32))]
      )),
      [FunDefinitionParameter(
        Type([], Pointer(Int32)),
        Decl(_)
      )],
      _
    )
  ])

test desugar function with return pointer [[
  int8 *g(int *x) {
    return x;
  }
]] run strip-annos to Program(_, [
    FunDefinition(
      _,
      [],
      Type([], Function(
        Type([], Pointer(Int8)),
        [Type([], Pointer(Int32))]
      )),
      _,
      _
    )
  ])
