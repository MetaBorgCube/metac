module MetaC-CSP/generate-c/process

imports

  BaseC/desugar/constructors
  signatures/MetaC-CSP-sig
  signatures/BaseC-sig
  MetaC-CSP/generate-c/alts
  names/MetaC-CSP/names

rules

  // Change process definitions, into a structure that are used for storing arguments.
  // and a function with the Process object as argument.
  generate01:
    CSPProcess(Identifier(name), CSPProcessParameterList(params), state, body)
    -> [
      TypedefDeclaration(
        Type([], Struct(
          None(),
          process-parameters
        )),
        Decl(Identifier(struct-name))
      ),
      <generate-alts-declarations> body,
      FunDefinition(
        Identifier(name),
        [],
        Type([], Function(
          Type([], Void()),
          [Type([], Pointer(TypedefName(Identifier("Process"))))]
        )),
        [FunDefinitionParameter(
          Type([], Pointer(TypedefName(Identifier("Process")))),
          Decl(Identifier("__csp_process__"))
        )],
        CompoundStatement(function-body)
      )
    ]
  where
    s-name := <strip-annos> name
    ; struct-name := <conc-strings> (s-name, "_ProcessArgs")
    ; states := <?CSPStateParameterList(<id>) <+ ![]> state
    ; all-params := <debug ; conc> (params, states)
    ; process-parameters := <map(generate-process-param-struct-field)> all-params
    ; process-parameters-variables := <map(generate-process-param-variable)> all-params
    ; function-body := <concat> [
        [VarDeclaration(
          [],
          Type([], Pointer(TypedefName(Identifier(struct-name)))),
          Decl(Identifier("args")),
          Some(PointerField(Var(Identifier("__csp_process__")), Identifier("args")))
        )],
        process-parameters-variables,
        [
          VarDeclaration(
            [], Type([], UInt8()),
            Decl(Identifier("__csp_process_recurse")), Some(Integer("0"))
          ),
          Do(
            body,
            Var(Identifier("__csp_process_recurse"))
          )
        ]
      ]

  generate-process-param-struct-field:
    FunDefinitionParameter(type, decl)
    -> StructDeclarator(type, decl, None())

  generate-process-param-variable:
    FunDefinitionParameter(type, decl@Decl(name))
    -> VarDeclaration([], type, decl, Some(
      PointerField(Var(Identifier("args")), name)
    ))

  generate: ExpressionStatement(Some(Call(var@Var(Identifier(_)), args))) ->
    <conc> (
      new-states,
      [
        ExpressionStatement(Some(Assign(Var(Identifier("__csp_process_recurse")), Assign(), Integer("1")))),
        Continue()
      ]
    )
    where
      CSPStateParameterList(states) := <get-process-state> var
      ; new-states := <zip(generate-assign-state)> (states, args)

  generate-assign-state:
    (FunDefinitionParameter(_, Decl(name)), arg)
    -> ExpressionStatement(Some(Assign(Var(name), Assign(), arg)))
