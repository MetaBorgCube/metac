module MetaC-CSP/generate-c

imports

  signatures/BaseC-sig
  signatures/MetaC-CSP-sig
  BaseC/desugar/constructors
  BaseC/generate/declarations
  runtime/types/-

rules

  generate-include-headers: CSPChan(_) -> Include("csp.h")

  generate02:
    Type(mods1, CSPChan(Type(mods2, _)))
    -> Type(merged-mods, Pointer(TypedefName(Identifier("Channel"))))
    where
      merged-mods := <conc ; uniq> (mods1, mods2)

  // Change chan<T> channel variable declarations
  generate01: 
    VarDeclaration(
      storage,
      type@Type(mods1, CSPChan(Type(mods2, _))),
      decl,
      None()
    ) ->
    VarDeclaration(
      storage,
      type,
      decl,
      Some(Call(
        Var(Identifier("CSP_chanAlloc")),
        [Var(Identifier("CSP_ONE2ONE")), Integer("0")]
      ))
    )

  // Change process definitions, into a structure that are used for storing arguments.
  // and a function with the Process object as argument.
  generate01:
    CSPProcess(Identifier(name), params, body)
    -> [
      TypedefDeclaration(
        Type([], Struct(
          None(),
          process-parameters
        )),
        Decl(Identifier(struct-name))
      ),
      FunDefinition(
        Identifier(name),
        [],
        Type([], Function(
          Type([], Void()),
          [Type([], Pointer(TypedefName(Identifier("Process"))))]
        )),
        [FunDefinitionParameter(
          Type([], Pointer(TypedefName(Identifier("Process")))),
          Decl(Identifier("p"))
        )],
        CompoundStatement(function-body)
      )
    ]
  where
    s-name := <strip-annos> name
    ; struct-name := <conc-strings> (s-name, "_ProcessArgs")
    ; process-parameters := <map(generate-process-param-struct-field)> params
    ; process-parameters-variables := <map(generate-process-param-variable)> params
    ; function-body := <concat> [
        [VarDeclaration(
          [],
          Type([], Pointer(TypedefName(Identifier(struct-name)))),
          Decl(Identifier("args")),
          Some(PointerField(Var(Identifier("p")), Identifier("args")))
        )],
        process-parameters-variables,
        [body]
      ]

  generate-process-param-struct-field:
    FunDefinitionParameter(type, decl)
    -> StructDeclarator(type, decl, None())

  generate-process-param-variable:
    FunDefinitionParameter(type, decl@Decl(name))
    -> VarDeclaration([], type, decl, Some(
      PointerField(Var(Identifier("args")), name)
    ))

  // add a temp variable in this scope that can be used by the CSPChanWrite
  // transformed code
  generate-temp-scope-variable:
    CSPChanWrite(_, e)
    -> VarDeclaration(
      [],
      Type([], Pointer(Void())),
      Decl(Identifier("__csp_chan_write_temp")),
      None()
    )

  generate02:
    CSPChanWrite(Var(chan-name), expr)
    -> Paren(
      Comma(
        Assign(Var(Identifier("__csp_chan_write_temp")), Assign(), expr),
        [],
        Call(
          Var(Identifier("CSP_chanOutCopy")),
          [
            Var(chan-name),
            Var(Identifier("__csp_chan_write_temp")),
            SizeofExpr(Var(Identifier("__csp_chan_write_temp")))
          ])))

  generate02:
    CSPChanRead(Var(chan-name), Var(dest-name))
    -> Call(
      Var(Identifier("CSP_chanInCopy")),
      [
        Var(chan-name),
        Var(dest-name),
        Negative(
          Cast(TypeName([Int()], None()),
            SizeofExpr(Var(dest-name))))
      ])
