module generate-c

imports

  signatures/BaseC-sig
  pp

strategies

  generate01 = fail
  generate02 = fail
  generate(|t) = fail
  generate = fail

  generate-include-headers = fail

  generate-includes:
    Program(x, decls)
    -> Program(x, <conc> (includes, decls))
    where
      includes := <collect-all(generate-include-headers)> decls

  generate-temp-scope-variable = fail

  generate-temp-scope-variables:
    CompoundStatement(stmts) -> CompoundStatement(
      <conc> (<vars>, stmts)
    )
    where
      rules(vars := [])
      ; <rec x(
        // Only analize this CompoundStatement, not the nested ones
        not(?CompoundStatement(_))
        ; (
          // check if some strategy succeeds
          generate-temp-scope-variable
          // add it to the other vars
          ; rules(vars := <union> (<vars>, [<id>]))
          // traverse other nodes
          <+ all(try(x))
        )
      )> stmts

  generate-pp =
    pp-debug // try to pretty print
    <+ debug // or print the AST

  generate-c: t@(selected, position, ast, path, project-path) -> (file-name, code)
    where
      file-name := <guarantee-extension(|"c")> path
      ; code := <
          try(generate-includes)
          ; bottomup(try(generate-temp-scope-variables))
          ; bottomup(try(generate01)   ; try(flatten-list))
          ; bottomup(try(generate02)   ; try(flatten-list))
          ; bottomup(try(generate(|t)) ; try(flatten-list))
          ; bottomup(try(generate)     ; try(flatten-list))
        ; generate-pp> selected
