module test-utils

imports

  BaseC/analysis/-
  BaseC/desugar/-
  BaseC/names/-
  names/BaseC/names/variables
  runtime/types/query
  signatures/BaseC/-
  editor-common.generated
  metac-runtime-utils/string

rules

  get-decl-from-program:
    Program(_, [decl]) -> <strip-annos> decl

  get-2nd-decl-from-program:
    Program(_, [_, decl]) -> <strip-annos> decl

  get-expr-from-program =
    find-elem(?Return(Some(x))) ; !x ; strip-annos

  get-fundef-from-program:
    Program(_, [fundef]) -> <strip-annos> fundef

  get-stmt-from-program:
    Program(_, [FunDef(_, _, _, CompoundStatement([stmt]))]) -> <strip-annos> stmt

  get-init-from-decl:
    VarDeclaration(_, _, _, _, Some(init)) -> <strip-annos> init
    
  get-init-from-decl:
    Declaration(_, [InitDeclarator(_, init)]) -> <strip-annos> init

  get-init-decl =
    get-decl-from-program ; get-init-from-decl

  get-2nd-init-decl =
    get-2nd-decl-from-program ; get-init-from-decl

  get-decl-type =
    get-decl-from-program
    ; ?Declaration([type], _) ; !type ; strip-annos

  get-expr-type =
    find-elem(?Return(Some(x)))
    ; <get-type> x

  test-utils-get-modifiers:
    Program(_, [VarDeclaration(_, _, _, Decl(Identifier(name)), _)]) -> <get-modifiers> name

  test-utils-get-storage:
    Program(_, [VarDeclaration(_, _, _, Decl(Identifier(name)), _)]) -> <get-storage> name

strategies

  get-type: Some(x) -> <get-type> x

  find-elem(s) =
    oncetd(s ; ?x) ; !x

  to-bool(s) =
    s ; !1 <+ !0

rules

  test-utils-relative-paths-get-program-file =
    oncetd(?Program(file, _))
    ; <test-make-rel-file> file

  test-utils-relative-paths-get-include =
    oncetd(?IncludeModule(file, _))
    ; <test-make-rel-file> file

  test-make-rel-file:
    file -> <strip-string-first> (<conc-strings> (<project-path>, "/"), file)
