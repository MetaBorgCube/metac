module MetaC/sm/syntax/sm

imports

  Common
  BaseC-Constants
  BaseC-Declarations
  BaseC-Identifiers
  BaseC-FunDef
  BaseC-Expr
  BaseC-Stmt

context-free syntax


  Declaration =
    StateMachine


  StateMachine.StateMachine =
    [
    statemachine [Identifier] {
      [InitState]
      [{EventOrDeclaration "\n"}*]
      [{State "\n"}+]
    }
    ]


  InitState.InitState =
    [init [Identifier];]


  EventOrDeclaration.Event =
    <event <Identifier>(<ParameterList>);> {prefer}

  EventOrDeclaration =
    Declaration


  State.State =
    [
    state [Identifier] {
      [Entry]
      [Transition*]
    }]


  Entry.None = ""

  Entry.Entry =
    [
    entry [CompoundStatement]
    ]

  Transition.Transition =
    [on [Identifier?] [Guard] [Effect] -> [Identifier];]


  Guard.None = ""

  Guard.Guard =
    <[<Expr>]>


  Effect.None = ""

  Effect.Effect =
    </ <Statement>>


  PostFixExpr.StateMachineTriggerEvent =
    [[PostFixExpr] <| [Identifier]([{AExpr ", "}*])]


  PostFixExpr.StateMachineField =
    [[PostFixExpr] |> [Identifier]]
