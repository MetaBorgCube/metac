module MetaC-StateMachine-names

imports

  include/Metac
  BaseC/trans/names/-
  MetaC/sm/trans/MetaC-StateMachine-constructors

namespaces

  StateMachine State Event

properties

  event-index of Event: EventIndex

binding rules

  StateMachine(Identifier(name), _, _, _, _):
    defines StateMachine name
    scopes Variable, Event, State

  InitState(Identifier(init)):
    refers to State init

  Event(index, Identifier(name), _):
    defines Event name
    of event-index index

  State(Identifier(state), _, _):
    defines State state

  Transition(None(), _, _, Identifier(state)):
    refers to State state

  Transition(Some(Identifier(event)), _, _, Identifier(state)):
    refers to Event event
    refers to State state

  StateMachineVariable(Identifier(sm-name), Identifier(var-name)):
    refers to StateMachine sm-name
    defines Variable var-name of type StateMachine(sm-name)

  StateMachineTriggerEvent(e, Identifier(event), params):
    refers to Event event in StateMachine s
      where e has type StateMachine(s)
